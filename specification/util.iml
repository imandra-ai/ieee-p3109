(** Some math utility functions *)

[@@@import ErrorPropagation, "dune:basic"]

open ErrorPropagation

let machine_idiv (x : int) (y : int) : int =
  let neg = (x < 0) <> (y < 0) in
  let r = (abs x) / (abs y) in
  if neg then -r else r

let rec pow2 (n : int) : real =
  if n > 0 then
    2.0 *. pow2 (n - 1)
  else if n < 0 then
    1.0 /. (2.0 *. (pow2 (- (n + 1))))
  else
    1.0
  [@@measure Ordinal.of_int (abs n)]

let rec pow (x : int) (n : int) : real =
  if (x = 0) then 0.0 else
  if (x = 2) then pow2 n else
  match n with
  | 0 -> 1.0
  | 1 -> Real.of_int x
  | _ when n > 1 ->
      let x2 = x * x in
      if n mod 2 = 0 then
        pow x2 (n / 2)
      else
        (Real.of_int x) *. (pow x2 ((n-1) / 2))
  | _ ->
      1.0 /. ((Real.of_int x) *. (pow x (- (n + 1))))
  [@@measure Ordinal.of_int (abs n)]

let rec ripow (x : real) (n : int) : real =
    if (n = 0) then 1.0 else
    if (x = 0.0) then 0.0 else
    if (x = 2.0) then pow2 n else
    if (n = 2) then x *. x else
    match n with
    | 0 -> 1.0
    | 1 -> x
    | _ when n > 1 ->
        let x2 = x *. x in
        if n mod 2 = 0 then
          ripow x2 (n / 2)
        else
          x *. (ripow x2 ((n-1) / 2))
    | _ ->
        1.0 /. (x *. (ripow x (- (n + 1))))
    [@@measure Ordinal.of_int (abs n)]

let rec ipow2 (x : int) : int =
  match x with
  | 0 -> 1
  | 1 -> 2
  | x -> if x <= 0 then 1 else 2 * ipow2 (x - 1)
  [@@unroll 5]

let ifloor (x : real) : int = Real.to_int x [@@macro]

let floor (x : real) : real = Real.of_int (ifloor x) [@@macro]

let rec floor_log2_abs (x : real) (max_depth : int) : int =
  if max_depth <= 0 then 0
  else (
    let x = Real.abs x in
    if x = 0.0 then 0
    else if x >. 1.0 then (
      let nx = x /. 2.0 in
      if nx >=. 1.0 then floor_log2_abs nx (max_depth - 1) + 1 else 0)
    else if x = 1.0 then 0
    else if x <. 1.0 then (
      let nx = x *. 2.0 in
      if nx <=. 1.0 then floor_log2_abs nx (max_depth - 1) - 1 else -1)
    else 0)
  [@@adm max_depth]
  [@@unroll 5]

let ( ^ ) (x : int) (y : int) : real = pow x y [@@macro]

let ( ^. ) (x : int) (y : int) = pow x y

let i2r = Real.of_int [@@macro]

let reciprocal (x : (real, string) Result.t) : (real, string) Result.t =
  match x with
  | Ok r when r = 0.0 -> Error "division by zero"
  | Ok r -> Ok (1.0 /. r)
  | Error e -> Error e

let rec num_bits (x : int) : int =
  if x = 0 then
    0
  else if x < 0 then
    1 + num_bits ((- x) / 2)
  else
    1 + num_bits (x / 2)
  [@@measure Ordinal.of_int (abs x)]

let rec factorial (x : int) : int =
  if x < 0 then
    - (x * (factorial ((- x) - 1)))
  else if x = 0 then
    1
  else
    x * factorial (x - 1)
  [@@measure Ordinal.of_int (abs x)]

let rec rfactorial (x : int) : real =
  if x < 0 then
    -. (Real.of_int x *. (rfactorial ((- x) - 1)))
  else if x = 0 then
    1.0
  else
    let t = rfactorial (x - 1) in
    (Real.of_int x *. t)
  [@@measure Ordinal.of_int (abs x)]

let rec sum (f : int -> int -> real) (from : int) (to_ : int) : real =
  if from > to_ then
    0.0
  else
    let cur = f from to_ in
    let rest = sum f (from + 1) to_ in
    cur +. rest
  [@@measure Ordinal.of_int (to_ - from)]

let rec product (f : int -> int -> real) (from : int) (to_ : int) : real =
  if from > to_ then
    1.0
  else
    let cur = f from to_ in
    let rest = product f (from + 1) to_ in
    cur *. rest
  [@@measure Ordinal.of_int (to_ - from)]

let rec rsum (f : int -> int -> (real, string) Result.t) (from : int) (to_ : int) : (real, string) Result.t =
  if from > to_ then
    Ok 0.0
  else
    let* cur = f from to_ in
    let* rest = rsum f (from + 1) to_ in
    Ok (cur +. rest)
  [@@measure Ordinal.of_int (to_ - from)]

let rec rproduct (f : int -> int -> (real, string) Result.t) (from : int) (to_ : int) : (real, string) Result.t =
  if from > to_ then
    Ok 1.0
  else
    let* cur = f from to_ in
    let* rest = rproduct f (from + 1) to_ in
    Ok (cur *. rest)
  [@@measure Ordinal.of_int (to_ - from)]