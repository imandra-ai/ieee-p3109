[@@@import Implementation, "dune:implementation"]
[@@@import Specification, "dune:specification"]
[@@@import Util, "dune:specification"]

open Implementation
open Util

let unroll_nonlin qt nqt steps = Tactic.unroll ~smt:"z3-full-nonlinear" ~query_timeout:qt ~no_asm_query_timeout:nqt steps

let i2s = Implementation.Float.i2s

let format_supported (f : Specification.Format.t) : bool =
  let open Specification in
  f.s = Signedness.Signed &&
  f.d = Domain.Extended &&
  Format.k f = 8

(** Proof that our implementation of [abs] is correct *)
theorem correct_abs (f : Specification.Format.t) (x : Float.t) =
  format_supported f ==>
  i2s (Implementation.Float.abs f x) = Specification.Float.abs f (i2s x)
  [@@by enumerate ["f"] @> repeat split_and @>>| auto]
  [@@timeout 3600]

(** Proof that our implementation of [negate] is correct *)
theorem correct_negate (f : Format.t) (x : Float.t) =
  format_supported f ==>
  i2s (Implementation.Float.negate f x) = Specification.Float.negate f (i2s x)
  [@@by enumerate ["f"] @> repeat split_and @>>| auto]
  [@@timeout 3600]

(** Proof that our implementation of [isZero] is correct *)
theorem correct_isZero (f : Format.t) (x : Float.t) =
  format_supported f ==>
  Implementation.Float.isZero f x = Specification.Float.isZero f (i2s x)
  [@@by enumerate ["f"] @> auto]
  [@@timeout 60]

(** Proof that our implementation of [isOne] is correct *)
theorem correct_isOne (f : Format.t) (x : Float.t) =
  format_supported f ==>
  Implementation.Float.isOne f x = Specification.Float.isOne f (i2s x)
  [@@by enumerate ["f"] @> auto]
  [@@timeout 60]

(** Proof that our implementation of [is_nan] is correct *)
theorem correct_isNaN (f : Format.t) (x : Float.t) =
  format_supported f ==>
  Implementation.Float.isNaN f x = Specification.Float.is_nan f (i2s x)
  [@@by enumerate ["f"] @> auto]
  [@@timeout 60]

(** Proof that our implementation of [isSignMinus] is correct *)
theorem correct_isSignMinus (f : Format.t) (x : Float.t) =
  format_supported f ==>
  Implementation.Float.isSignMinus f x = Specification.Float.isSignMinus f (i2s x)
  [@@by enumerate ["f"] @> auto]
  [@@timeout 60]

(** Proof that our implementation of [isNormal] is correct *)
theorem correct_is_normal (f : Format.t) (x : Float.t) =
  format_supported f ==>
  Implementation.Float.isNormal f x = Specification.Float.isNormal f (i2s x)
  [@@by enumerate ["f"] @> auto]
  [@@timeout 60]

(** Proof that our implementation of [isSubnormal] is correct *)
theorem correct_is_subnormal (f : Format.t) (x : Float.t) =
  format_supported f ==>
  Implementation.Float.isSubnormal f x = Specification.Float.isSubnormal f (i2s x)
  [@@by enumerate ["f"] @> auto]
  [@@timeout 60]
